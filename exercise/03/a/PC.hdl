// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in = feedback, out = pc);//一開始從register抓一個 跟＋1

    Mux16(a = feedback, b = pc, sel = inc, out = w0);//問他說要不要＋1
    Mux16(a = w0, b = in, sel = load, out = w1);//要不要抓給他的值
    Mux16(a = w1, b = false, sel = reset, out = cout);//有重設就全部變0所以要最後
    
    Register(in = cout, load = true, out = out, out = feedback);
}
